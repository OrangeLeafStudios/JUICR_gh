// fileUtils.src

fs_rm = function(pathStr, isRecursive=false)
	pathFile = get_abs_path(pathStr)

	file = get_shell.host_computer.File(pathFile)
	if file == null then return "rm: file not found: " + pathFile
	if not file.has_permission("w") then return "rm: permission denied"

	if file.is_folder == 1 and isRecursive == 0 and file.is_symlink == 0 then
		return "rm: " + file.name + " is a directory"
	end if

	output = file.delete
	if output != null and output.len > 0 then return output
	return 1
end function

fs_touch = function(pathStr)

	pathFile = get_abs_path(pathStr)
	pathParent = parent_path(pathFile)
	computer = get_shell.host_computer

	if pathParent == pathFile then
		pathParent = current_path
	end if

	parent = computer.File(pathParent)
	if not parent then return "touch: " + pathParent + " not found"
	if not parent.has_permission("w") then return "touch: permission denied"

	arrayPath = pathFile.split("/")
	output = computer.touch(parent.path, arrayPath[arrayPath.len - 1])
	if output and output != 1 then return output
	return 1
end function

fs_mkdir = function(pathStr)

	computer = get_shell.host_computer
	pathFile = get_abs_path(pathStr)
	pathParent = parent_path(pathFile)
	existFile = computer.File(pathFile)

	if pathParent == pathFile then
		pathParent = current_path
	end if

	parent = computer.File(pathParent)
	if parent == null then
		return "mkdir: " + pathParent + " not found"
	else if existFile != null then
		return "mkdir: " + existFile.path + " file exists"
	else if not parent.has_permission("w") then
		return "mkdir: permission denied"
	else
		arrayPath = pathFile.split("/")
		output = computer.create_folder(parent.path, arrayPath[arrayPath.len - 1])
		if output != null and output != 1 then return output
	end if

	return 1
end function

fs_rmdir = function(pathStr)

	pathFile = get_abs_path(pathStr)
	computer = get_shell.host_computer
	file = computer.File(pathFile)

	if file == null then return "rmdir: " + pathFile + " not found"
	if not file.has_permission("w") then return "rmdir: permission denied"
	if file.is_folder != 1 then return "rmdir: " + file.name + " is not a directory"
	if file.is_symlink == 1 then

		output = file.delete
		if output != null and output.len > 0 then return output
		return 1
	end if

	output = file.delete
	if output != null and output != 1 then return output
	return 1
end function

SaveExploits = function()
	cacheFolder = get_shell.host_computer.File("/.juicr_cache")
	CachePrep()
	exploitFile = cacheFolder.File("/exploits.cache")
	if exploitFile == null then
		get_shell.host_computer.create_file(cacheFolder.path, "exploits.cache", "")
		exploitFile = cacheFolder.File("/exploits.cache")
	end if
	exploitData = JSON.stringify(mapExploit)
	exploitFile.set_content(exploitData)
end function

LoadExploits = function()
	cacheFolder = get_shell.host_computer.File("/.juicr_cache")
	exploitFile = cacheFolder.File("/exploits.cache")
	if exploitFile == null then
		return {}
	end if
	exploitData = exploitFile.get_content()
	if exploitData.len == 0 then
		return {}
	end if
	mapExploitLoaded = JSON.parse(exploitData)
	return mapExploitLoaded
end function